buildscript {
	repositories {
		mavenCentral()
	}

	dependencies {
		classpath 'org.springframework.boot:spring-boot-gradle-plugin:2.0.3.RELEASE'
		classpath 'io.spring.gradle:dependency-management-plugin:1.0.5.RELEASE'
		classpath 'se.transmode.gradle:gradle-docker:1.2'
	}
}

apply plugin: 'java'
apply plugin: 'org.springframework.boot'
apply plugin: 'docker'
apply plugin: 'io.spring.dependency-management'

def springCloudVersion = 'Hoxton.SR1'

sourceCompatibility = project.javaVersion
targetCompatibility = project.javaVersion

dependencyManagement {
	imports {
		mavenBom 'org.springframework.cloud:spring-cloud-config:2.0.0.RELEASE'
		mavenBom "org.springframework.cloud:spring-cloud-starter-parent:${springCloudVersion}"
		mavenBom "org.springframework.cloud:spring-cloud-dependencies:${springCloudVersion}"
	}
}

project.ext.gcloudBuildDir = file('build/gcloud-build/')
project.ext.gcloudProjectId = 'nlacombe-net-prod'

String gcloudDockerImageUrl

dependencies {
	implementation project(':vault-api')

	implementation "net.nlacombe:ws-utils:${depVersions.wsUtils}"
	implementation 'net.nlacombe:auth-lib:10.0.0'
	implementation 'net.nlacombe:common-lib:11.0.0'

	implementation 'org.springframework.cloud:spring-cloud-starter-config'
	implementation 'org.springframework.cloud:spring-cloud-gcp-starter'
	implementation 'org.springframework.cloud:spring-cloud-gcp-starter-sql-mysql'
	implementation "org.springframework.boot:spring-boot-starter-web"
	implementation 'org.springframework.boot:spring-boot-starter-actuator'
	implementation 'org.springframework.boot:spring-boot-starter-data-jpa'
	implementation 'org.springframework.boot:spring-boot-starter-security'
	implementation 'org.apache.cxf:cxf-spring-boot-starter-jaxrs:3.2.6'
	implementation 'org.flywaydb:flyway-core:5.1.4'
	implementation 'javax.inject:javax.inject:1'
	implementation 'com.fasterxml.jackson.jaxrs:jackson-jaxrs-json-provider:2.9.6'
	implementation 'com.fasterxml.jackson.datatype:jackson-datatype-jsr310:2.9.6'

	implementation 'org.slf4j:slf4j-api:1.7.25'

	runtimeOnly 'mysql:mysql-connector-java:8.0.12'
	runtimeOnly 'ch.qos.logback:logback-classic:1.2.3'
	runtimeOnly 'javax.xml.bind:jaxb-api:2.3.0'
	runtimeOnly 'javax.activation:activation:1.1.1'
	runtimeOnly 'org.javassist:javassist:3.24.1-GA'

	testImplementation 'junit:junit:4.12'
	testImplementation 'org.assertj:assertj-core:3.8.0'
	testImplementation 'org.mockito:mockito-core:2.24.5'
}

task buildDocker(type: Docker, dependsOn: assemble) {
	tag = "${project.name}"
	dockerfile = file('src/main/docker/Dockerfile')
	doFirst {
		copy {
			from jar
			from file('src/main/docker/')
			into stageDir
		}
	}
}

task buildGcloudDockerImage(type: Exec, dependsOn: assemble) {
	def buildStartTime = new Date()
	gcloudDockerImageUrl = getGcloudDockerImageUrl(buildStartTime)

	workingDir project.gcloudBuildDir
	commandLine 'gcloud', 'builds', 'submit', '--tag', gcloudDockerImageUrl

	doFirst {
		copy {
			from jar
			from file('src/main/docker/')
			into project.gcloudBuildDir
		}
	}
}

task deployToProdGcloud(type: Exec, dependsOn: buildGcloudDockerImage) {
	workingDir project.gcloudBuildDir
	commandLine 'gcloud', 'beta', 'run', 'deploy', "${project.name}",
			'--region', 'us-east4',
			'--platform', 'managed',
			'--memory', '500M',
			'--image', gcloudDockerImageUrl
}

def getGcloudDockerImageUrl(date) {
	def version = project.version + '-' + getDateTimestamp(date)

	return "gcr.io/${project.gcloudProjectId}/${project.name}:${version}"
}

def static getDateTimestamp(date) {
	return date.format('yyyyMMddHHmmss')
}
